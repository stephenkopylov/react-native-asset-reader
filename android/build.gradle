buildscript {
  if (project == rootProject) {
    repositories {
      google()
      mavenCentral()
      jcenter()
      maven { url "https://nexus.eoadm.com/repository/maven-releases/" }
    }

    dependencies {
      classpath 'com.android.tools.build:gradle:3.5.3'
    }
  }
}

apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
  // See https://github.com/facebookincubator/fbjni/issues/50
  buildFeatures {
    prefab true //important for JSI
  }
  dependencies {
    implementation 'com.facebook.fbjni:fbjni:0.3.0' //important for JSI
  }
  configurations {
    all*.exclude module: 'fbjni-java-only' //important for JSI
  }

  compileSdkVersion safeExtGet('RNExpertOptionMobileSocketModule_compileSdkVersion', 29)
  defaultConfig {
    minSdkVersion safeExtGet('RNExpertOptionMobileSocketModule_minSdkVersion', 16)
    targetSdkVersion safeExtGet('RNExpertOptionMobileSocketModule_targetSdkVersion', 29)
    versionCode 1
    versionName "1.0"

    externalNativeBuild {
      cmake {
        cppFlags "-O2 -frtti -fexceptions -Wall -fstack-protector-all"
        abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        arguments "-DANDROID_STL=c++_shared" //important for JSI
      }
    }
  }

  buildTypes {
    debug {
      packagingOptions {
        doNotStrip '**/*.so'
      }
      externalNativeBuild {
        cmake {
          arguments "-DCMAKE_BUILD_TYPE=Debug"
        }
      }
    }

    release {
      packagingOptions {
        doNotStrip '**/*.so'
      }
      externalNativeBuild {
        cmake {
          arguments "-DCMAKE_BUILD_TYPE=RelWithDebInfo"
        }
      }
    }
  }

  externalNativeBuild {
    cmake {
      path "CMakeLists.txt"
    }
  }

  lintOptions {
    disable 'GradleCompatible'
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  flavorDimensions "type"

  productFlavors {
    aar {
      dimension "type"
    }
    local {
      dimension "type"
    }
  }

  packagingOptions {
    excludes = [
      "**/libc++_shared.so",
      "**/libfbjni.so",
      "**/libjsi.so",
      "**/libfolly_json.so",
      "**/libglog.so",
      "**/libhermes.so",
      "**/libreactnativejni.so",
      "**/libjscexecutor.so",
      "**/libturbomodulejsijni.so"
    ]
  }
}

repositories {
  mavenLocal()
  maven {
    url("$rootDir/../node_modules/react-native/android")
  }
  maven { url "https://nexus.eoadm.com/repository/maven-releases/" }
  google()
  mavenCentral()
  jcenter()
}

dependencies {
  implementation "com.facebook.react:react-native:+"
  aarImplementation('com.eo:ExpertOptionMobileSocket:3.0.5@aar') {
    transitive = true
  }

  localImplementation project(':expertoption-mobile-socket')
}

android.defaultConfig.ndk.debugSymbolLevel = 'FULL'
